plugins {
	id "groovy"
	id "com.bmuschko.clover" version "2.2.1"
	id "org.sonarqube" version "2.6.2"
}

group = "com.disney.dss.dpe"
version = "1.0.0"

repositories {
	mavenCentral()
}

dependencies {
	compile "org.codehaus.groovy:groovy-all:2.5.0"

	testCompile "com.lesfurets:jenkins-pipeline-unit:1.1"
	testCompile "junit:junit:4.12"
	// testCompile "org.apache.logging.log4j:log4j-slf4j-impl:2.8.1"

	// runtime "org.apache.logging.log4j:log4j-core:2.8.1"
	// runtime "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.8.6"
	// runtime "com.fasterxml.jackson.core:jackson-databind:2.8.6"

	clover 'org.openclover:clover:4.2.0'
}

sourceSets {
	main {
		groovy {
			srcDirs "vars"
		}
	}
}

clover {
	report {
		//html = true
		xml = true
	}
}

sonarqube {
	properties {
		property "sonar.clover.reportPath", "$buildDir/reports/clover/clover.xml"
	}
}

test {
    // delete old test reports
    dependsOn cleanTest

    // don't stop if tests fail
    ignoreFailures = true

    // minimize logging
    testLogging.maxGranularity = 0

    // show stdout from tests
    onOutput { dest, event -> print event.message }

    // show test results
    def results = []
    afterTest { desc, result -> println "${desc.className.split("\\.")[-1]}: ${desc.name}: ${result.resultType}" }
    afterSuite { desc, result -> if (desc.className) { results << result } }

    // show summary
    doLast {
		println "Tests: ${results.sum { it.testCount }}, Failures: ${results.sum { it.failedTestCount }}" +
      	", Errors: ${results.sum { it.exceptions.size() }}, Skipped: ${results.sum { it.skippedTestCount }}"
	}
}
